apply plugin: 'com.android.application'

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "ee.eki.ekisynt2"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1204
        versionName '1.2.04'

        externalNativeBuild {
            cmake {
                cppFlags "-frtti -fexceptions"
            }
        }
    }

    signingConfigs {
        release {
            storeFile file(System.getenv("KEYSTORE_EKISPEAK"))
            keyAlias System.getenv("KEYSTORE_EKISPEAK_ALIAS")
        }
    }

    buildTypes {
        debug {
            debuggable false
        }
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            debuggable false
        }
    }
    lintOptions {
        disable 'GoogleAppIndexingWarning'
    }
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }
}


if (project.hasProperty('storePassword') &&
        project.hasProperty('keyPassword')) {
    android.signingConfigs.release.storePassword = storePassword
    android.signingConfigs.release.keyPassword = keyPassword
} else {
    android.buildTypes.release.signingConfig = null
}

task deploy(type: Exec) {
    description 'Copy the APK to the deploy directory'

    def deploy_dir = System.getenv('APK_DEPLOY_DIR')

    def version = android.defaultConfig.versionName

    def outputs = 'build/outputs/'
    def apk1 = outputs + 'apk/release/app-release.apk'
    def apk2 = "${deploy_dir}EKISpeak-${version}.apk"

    commandLine 'cp', '--verbose', apk1, apk2

    // TODO: how to add multiple commands?
    // commandLine 'ls', '-l', deploy_dir

    println "adb uninstall ee.eki.ekisynt2"
    println "adb install -r ${apk2}"
}

dependencies {
    implementation 'com.android.support:appcompat-v7:28.0.0'
}
