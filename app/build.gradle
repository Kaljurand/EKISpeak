apply plugin: 'com.android.application'

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    defaultConfig {
        applicationId "ee.eki.ekisynt"
        minSdkVersion 14
        targetSdkVersion 27
        versionCode 1202
        versionName '1.2.02'

        externalNativeBuild {
            cmake {
                cppFlags "-frtti -fexceptions"
            }
        }
    }

    signingConfigs {
        release {
            storeFile file(System.getenv("KEYSTORE_EKISPEAK"))
            keyAlias System.getenv("KEYSTORE_EKISPEAK_ALIAS")
        }
    }

    buildTypes {
        debug {
            debuggable false
        }
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            debuggable false
        }
    }
    lintOptions {
        disable 'GoogleAppIndexingWarning'
    }
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }
}


if (project.hasProperty('storePassword') &&
        project.hasProperty('keyPassword')) {
    android.signingConfigs.release.storePassword = storePassword
    android.signingConfigs.release.keyPassword = keyPassword
} else {
    android.buildTypes.release.signingConfig = null
}

task(deploy) << {
    description 'Copy the APK to the deploy directory'

    def deploy_dir = System.getenv('APK_DEPLOY_DIR')

    def version = android.defaultConfig.versionName

    def outputs = 'build/outputs/'
    def apk1 = outputs + 'apk/release/app-release.apk'
    def apk2 = "${deploy_dir}EKISpeak-${version}.apk"

    def output1 = exec {
        commandLine 'cp', '--verbose', apk1, apk2
    }

    def output3 = exec {
        commandLine 'ls', '-l', deploy_dir
    }

    println "adb uninstall ee.eki.ekisynt"
    println "adb install ${apk2}"
}

dependencies {
    implementation 'com.android.support:appcompat-v7:27.1.1'
}
